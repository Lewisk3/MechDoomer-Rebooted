class MechLabCursor : MenuCursor {}

// Hardpoint Container
class MechHardpointContainer : MenuElement
{
	int partID;
	MechMenu_Mechlab mechlab;
	MechHardpoint hardpoint;
	Array<MechItemSlot> slots;
	double slotsWidth, slotsHeight;
	double itemSpacing;
	string containerName;
	
	Array<MechlabSquareButton> buttons;
		
	virtual void Setup(string title, MechHardpoint hardpoint, double slotsWidth, double slotsHeight)
	{
		self.hardpoint = hardpoint;
		self.slotsHeight = slotsHeight;
		self.slotsWidth = slotsWidth;
		self.containerName = title;
		mechlab = MechMenu_Mechlab(parentMenu);
		itemSpacing = 0; //slotsHeight * (1/8.);
		
		if(!mechlab.mech) return;
		
		// Find hardpoint partID
		for(int i = 0; i < mechlab.mech.parts.Size(); i++)
		{
			let hp = MechHardpoint(mechlab.mech.parts[i]);
			if(hp == hardpoint) 
			{
				partID = i;
				break;
			}
		}
		
		vector2 btnPos = pos + (0,-94);
		double btnSize = 12;
		double btnDist = 20;
		
		double xPos = hardpoint.hasRearArmor ? size.x*0.45 : size.x*0.60;
		btnPos.x = pos.x+xPos;
		
		let armorPlusButton = MechlabSquareButton(
			MenuElement.Create("MechlabSquareButton", "ArmorUP", parentMenu, btnPos, (btnSize,btnSize))
		);
		let armorMinusButton = MechlabSquareButton(
			MenuElement.Create("MechlabSquareButton", "ArmorDOWN", parentMenu, btnPos + (0,btnDist), (btnSize,btnSize))
		);
		
		if(hardpoint.hasRearArmor)
		{
			btnPos.x = pos.x + (size.x*0.80);
			let rearArmorPlusButton = MechlabSquareButton(
				MenuElement.Create("MechlabSquareButton", "rearArmorUP", parentMenu, btnPos, (btnSize,btnSize))
			);
			let rearArmorMinusButton = MechlabSquareButton(
				MenuElement.Create("MechlabSquareButton", "rearArmorDOWN", parentMenu, btnPos + (0,btnDist), (btnSize,btnSize))
			);
			
			rearArmorPlusButton.Setup("+");
			rearArmorMinusButton.Setup("-");
			buttons.push(rearArmorPlusButton);
			buttons.push(rearArmorMinusButton);
		}
		
		armorPlusButton.Setup("+");
		armorMinusButton.Setup("-");
		buttons.push(armorPlusButton);
		buttons.push(armorMinusButton);
		
		// Create all slots
		for(int i = 0; i < hardpoint.maxslots; i++)
		{
			let menuSlot = MechItemSlot(MenuElement.Create(
				"MechItemSlot", String.Format("Slot#%d", i), 
				parentMenu, pos, (slotsWidth,slotsHeight)
			));
			menuSlot.container = self;
			slots.Push(menuSlot);
		}
	}
	
	override void Update(double dT)
	{	
		// Update all slot item pointers.
		int fillSlot = 0;
		for(int i = 0; i < hardpoint.items.Size(); i++)
		{
			if(hardpoint.items[i] is "MechDynamicStructure") continue;
			
			let curSlot = slots[fillSlot];
			let prevItem = curSlot.slotItem;
			curSlot.slotItem = hardpoint.items[i];
			curSlot.Update(dT);
			
			fillSlot++;
			
			// Mouse collisions
			mechlab.cursor.CheckCollision(curSlot);
		}	
		
		// Setup slots for dynamic structures
		for(int i = 0; i < hardpoint.items.Size(); i++)
		{
			if( !(hardpoint.items[i] is "MechDynamicStructure") ) continue;
			
			let curSlot = slots[fillSlot];
			let prevItem = curSlot.slotItem;
			curSlot.slotItem = hardpoint.items[i];
			curSlot.Update(dT);
			
			fillSlot++;
			
			// Mouse collisions
			mechlab.cursor.CheckCollision(curSlot);
		}
				
		size.y = 1 + (((slotsHeight*slots[0].scale) + itemSpacing) * slots.Size());
	}
	
	override void Draw(double dT)
	{
		if(!mechlab) return;
		
		double wScale = slotsWidth / 256.;
		
		// Draw top-bar
		vector2 topBarPos = pos - (0,130);
		double topBarHeight = 120;
		
		MWDrawer.DrawImage(
			"Graphics/Mechlab/itemTop.png", 
			topBarPos - (0,3), scale:(-wScale*0.45, 0.6), 
			tint:0, texoffs:(2,1)
		);
		
		MWDrawer.DrawString("SmallFont", containerName, topBarPos + (5,-10), translation:Font.CR_BLACK, scale:(0.8,1.0)); 
		
		Array<HardpointMount> energyPoints, ballisticPoints, missilePoints;
		hardpoint.getMountPoints(energyPoints, "Energy");
		hardpoint.getMountPoints(ballisticPoints, "Ballistic");
		hardpoint.getMountPoints(missilePoints, "Missile");
		
		double frontArmor = floor(hardpoint.armorPoints);
		double rearArmor = floor(hardpoint.hasRearArmor ? hardpoint.rearArmor : -1);
		
		MWDrawer.Fill("Black", topBarPos.x,topBarPos.y + 8, size.x, topBarHeight);
		MWDrawer.DrawRect(topBarPos + (0,8), (size.x,topBarHeight), "Gray");
		
		MWDrawer.Fill("DarkGray", topBarPos.x,topBarPos.y + 9, size.x, 20);
		MWDrawer.DrawString("ConsoleFont", "ARMOR", topBarPos + (5,9), translation:Font.CR_GRAY); 
		
		// Armor Points
		double reserveArmorPoints = ceil(hardpoint.maxArmor - (max(0,frontArmor) + max(0, rearArmor)));
		MWDrawer.DrawString("ConsoleFont", ""..int(reserveArmorPoints), topBarPos + (160,9), translation:Font.CR_BLUE); 
		
		MWDrawer.DrawString("ConsoleFont", ""..int(frontArmor), topBarPos + (rearArmor >= 0 ? size.x*0.25 : size.x*0.4,45));
		if(rearArmor >= 0)
			MWDrawer.DrawString("ConsoleFont", ""..int(rearArmor), topBarPos + (size.x*0.6,45));
		
		// Weapon types
		MWDrawer.Fill("DarkGray", topBarPos.x,topBarPos.y + 80, size.x, 20);
		MWDrawer.DrawString("ConsoleFont", "STRUCTURE", topBarPos + (5,80), translation:Font.CR_GRAY); 
		MWDrawer.DrawString("ConsoleFont", ""..int(ceil(hardpoint.healthPoints)), topBarPos + (160,80), translation:Font.CR_RED); 
		
		double iconScale = 0.60;
		MWDrawer.DrawImage("Graphics/mechlab/energyIcon.png", topBarPos + (30,113), scale:(iconScale,iconScale));
		MWDrawer.DrawString("ConsoleFont", ""..int(energyPoints.Size()), topBarPos + (45,105), translation:Font.CR_GREEN); 
		
		MWDrawer.DrawImage("Graphics/mechlab/ballisticIcon.png", topBarPos + (95,113), scale:(iconScale,iconScale));
		MWDrawer.DrawString("ConsoleFont", ""..int(ballisticPoints.Size()), topBarPos + (105,105), translation:Font.CR_GREEN); 
		
		MWDrawer.DrawImage("Graphics/mechlab/missileIcon.png", topBarPos + (155,113), scale:(iconScale,iconScale));
		MWDrawer.DrawString("ConsoleFont", ""..int(missilePoints.Size()), topBarPos + (170,105), translation:Font.CR_GREEN); 
		
		// Background
		MWDrawer.Fill("Black", pos.x,pos.y, size.x, size.y);
		MWDrawer.DrawRect(pos, size, "Gray");
				
		double drawY = 0;
		double slotSpacing = (slotsHeight*slots[0].scale) + itemSpacing;
		for(int i = 0; i < hardpoint.maxslots; i++)
		{
			MWDrawer.DrawString("ConsoleFont", "-", (pos.x + 8, pos.y + drawY));
			MWDrawer.DrawThickLine(pos.x, pos.y + drawY + slotSpacing, pos.x + size.x, pos.y + drawY + slotSpacing, 2, "Gray"); 
			drawY += slotSpacing;
		}
		
		// Buttons
		for(int i = 0; i < buttons.Size(); i++)
		{
			let btn = buttons[i];	
			
			if( btn.Tag.IndexOf("UP") != -1 )
				btn.disabled = reserveArmorPoints <= 0;
				
			if( btn.Tag ~== "ArmorDOWN")
				btn.disabled = hardpoint.armorPoints <= 0;
			if( btn.Tag ~== "rearArmorDOWN")
				btn.disabled = hardpoint.rearArmor <= 0;
				
			if(btn.pressed)
			{
				if(btn.Tag ~== "ArmorUP")
				{
					EventHandler.SendNetworkEvent(
						String.Format("mechlab.armor.front.add.%d.%d", partID, 1)
					);
				}
				else if(btn.Tag ~== "rearArmorUP")
				{
					EventHandler.SendNetworkEvent(
						String.Format("mechlab.armor.rear.add.%d.%d", partID, 1)
					);
				}
				else if(btn.Tag ~== "ArmorDOWN")
				{
					EventHandler.SendNetworkEvent(
						String.Format("mechlab.armor.front.remove.%d.%d", partID, 1)
					);
				}
				else if(btn.Tag ~== "rearArmorDOWN")
				{
					EventHandler.SendNetworkEvent(
						String.Format("mechlab.armor.rear.remove.%d.%d", partID, 1)
					);
				}
				btn.pressed = false;
			}
			
			btn.Draw(dT);
			btn.Update(dT);
			
			// Mouse collisions
			mechlab.cursor.CheckCollision(btn);
		}
		
		// Update slot draw positions
		drawY = 0;
		for(int i = 0; i < hardpoint.items.Size(); i++)
		{	
			let slot = slots[i];
			// Skip if item is being dragged.
			if(mechlab.cursor.draggingElement == slot) continue;
			
			if(slot.slotItem)
			{
				slot.pos.x = pos.x;
				slot.pos.y = pos.y + drawY;
				slot.Draw(dT);
				drawY += (slotSpacing * max(1, slot.slotItem.size));
				if(drawY < size.y-12) 
					MWDrawer.DrawThickLine(pos.x, pos.y + drawY, pos.x + size.x, pos.y + drawY, 4, "Black", slot.alpha); 
			}
		}
		
	}
}

class MechItemSlot : MenuElement
{
	MechHardpointContainer container;
	vector2 defaultPos;
	MechItem slotItem;
	double slotHeight;
	double scale;
	double alpha;

	override void Init()
	{
		super.Init();
		defaultPos = pos;
		slotHeight = size.y;
		scale = 0.5;
		alpha = 1.0;
	}
	
	static Color, Color GetItemColors(MechItem item)
	{
		Color slotColor = 0;
		uint itemFontColor = Font.CR_WHITE;
		if(!item) return itemFontColor, slotColor;
		
		bool isDynamic = item is "MechDynamicStructure";
		if(!item.movable && !isDynamic)
		{
			itemFontColor = Font.CR_BLACK;
		}
		else
		{
			let weap = MechWeapon(item);
			if(weap)
			{
				if(weap.mountType ~== "energy"   ) slotColor = 0xA0FFFF00;
				if(weap.mountType ~== "ballistic") slotColor = 0xA00000FF;
				if(weap.mountType ~== "missile"  ) slotColor = 0xA000FF00;
			}
			
			if(isDynamic)
			{
				itemFontColor = Font.CR_BLACK;
				slotColor = 0xB2000000;
			}
			
			if(item is "MechAmmo")
				slotColor = 0xA0FF00FF;
			
			if(item is "MechEngine")
				slotColor = 0xA0FF0000;
		}
		
		return itemFontColor, slotColor;
	}
	
	override void Draw(double dT)
	{
		Font itemFont = "SmallFont"; 
		
		Color slotColor = 0;
		uint itemFontColor = Font.CR_WHITE;
		double wScale = size.x / 256;
		
		[itemFontColor, slotColor] = GetItemColors(slotItem);
		MWDrawer.DrawImage("Graphics/Mechlab/itemTop.png", pos, alpha:alpha, scale:(scale*wScale,scale), tint:slotColor, texoffs:(0,0));
		
		if(slotItem)
		{
			int itemSize = max(1,slotItem.size);
			double scaledSlotHeight = slotHeight * scale;
			for(int i = 0; i < itemSize-1; i++)
			{
				MWDrawer.DrawImage(
					"Graphics/Mechlab/itemBottom.png", 
					pos + (0, scaledSlotHeight + (i * scaledSlotHeight)), 
					alpha:alpha, scale:(scale*wScale,scale), tint:slotColor, 
					texoffs:(0,0)
				);
			}
			
			// Draw item data
			double fontScale = scale * 1.75;
			vector2 centerPos = pos + (
				(size.x / 2.0),
				(size.y / 2.0) - ( (itemFont.GetHeight() * (2.0*fontScale) ) / 2.0 )
			);
			
			MWDrawer.DrawString(
				itemFont, slotItem.displayName, 
				centerPos, MWDrawer.SS_TEXT_CENTER, itemFontColor,
				alpha, scale:(fontScale, fontScale)
			);
		}
		
		// drawCollider();
	}
	
	override bool checkDraggable()
	{
		return slotItem && slotItem.movable;
	}
	
	override void updateDragPos(MenuCursor cursor)
	{
		pos = cursor.pos - (
			(size.x / 2.0) - (cursor.size.x / 2.0), 
			(size.y / 2.0) + 4
		);
	}
	
	override void OnDrop(MenuCursor cursor)
	{
		let mechlab = MechMenu_MechLab(parentMenu);
		if(!mechlab || !cursor) return;
		
		let mouseOverContainer = MechHardpointContainer(cursor.getCollidingWithType("MechHardpointContainer"));
		string itemIDs = mechlab.mech.getMechItemIndex(slotItem);
				
		if(mouseOverContainer == container) 
		{
			// Drop
			pos = defaultPos; 
			return;
		}
		else if(mouseOverContainer)
		{
			// Move
			pos = defaultPos; 
			EventHandler.SendNetworkEvent(String.Format("mechlab.item.move.%s.%d", itemIDs, mouseOverContainer.partID));
			return;
		}
		else
		{
			// Dequip
			EventHandler.SendNetworkEvent(String.Format("mechlab.item.dequip.%s", itemIDs));
		}
	}
	
	override void Update(double dT)
	{
		// Update size to item slot size.
		double scaledSlotHeight = slotHeight * scale;
		size.y = slotItem ? scaledSlotHeight * max(1,slotItem.size) : scaledSlotHeight;
	}
}

// Inventory container
class MenuItemsInvContainer : MenuElement
{
	MechMenu_Mechlab mechlab;
	Actor driver;
	Array<MechInvItem> invItems;
	double scroll, scrollVel, scrollMax;
	MechContainerScrollBar scrollBar;
	
	override void Init()
	{
		mechlab = MechMenu_Mechlab(parentMenu);
		if(!mechlab || !mechlab.mech) return;
		
		driver = mechlab.mech.driver;
		scrollBar = MechContainerScrollBar(
			MenuElement.Create("MechContainerScrollBar", "invScrollBar", parentMenu, pos+(425,0), (32,0))
		);
		scrollBar.Setup(self);
	}
	
	override void Draw(double dT)
	{
		if(!driver) return;
		
		let inv = I_MechItemStorage(driver.FindInventory("I_MechItemStorage"));
		if(!inv) return;
			
		double itemSpacing = 16;
		double startY = 20;
		double curY = (pos.y + startY) - scroll;
		bool doScrollEnd = false; // If true, scrolls to the end of items list.
		
		// Update items list
		int curSize = invItems.Size();
		int newSize = inv.items.Size();
		if(curSize != newSize)
		{
			if(newSize > curSize) doScrollEnd = true;
			invItems.Resize(inv.items.Size());
		}
		
		MWDrawer.SetClipRect(pos.x,pos.y,size.x,size.y);
		for(int i = 0; i < invItems.Size(); i++)
		{
			let item = invItems[i];
			if(!item)
			{	
				item = MechInvItem(MenuElement.Create(
					"MechInvItem", 
					String.Format("InvItem#%d",i), 
					parentMenu, pos + (size.x*0.30, 0), (size.x * 0.5, 16)
				));
				invItems[i] = item;
			}
			
			item.item = inv.items[i];
			item.itemID = i;
			
			bool dragging = mechlab.cursor.draggingElement == item;
			bool outOfBounds = !item.CheckCollision(self, false);
			item.outOfBounds = outOfBounds;
			
			if(!dragging)
			{
				item.pos.y = curY;			
				if(!outOfBounds) item.Draw(dT);
			}
			else
			{	
				// Redraw item but, grayed out.
				vector2 oldPos = item.pos;
				item.grayed = true;
				item.pos.x = item.defaultPos.x;
				item.pos.y = curY;
				item.Draw(dT);
				item.grayed = false;
				item.pos = oldPos;
			}
			
			// Needs to be called after Draw
			curY += (item.size.y + itemSpacing);
			
			if(!outOfBounds) item.Update(dT);
			mechlab.cursor.CheckCollision(item);
		}
		MWDrawer.ClearClipRect();
		
		// Find scroll height
		double pageHeight = size.y;
		double maxY = pos.y + startY;
		for(int i = 0; i < invItems.Size(); i++)
		{
			let item = invItems[i];
			if(!item) continue;
			
			maxY += (item.size.y + itemSpacing);
		}
		
		// Scrollbar Element
		scrollBar.Update(dt);
		mechlab.cursor.CheckCollision(scrollBar);
		
		scrollMax = maxY - pageHeight - pos.y;
		scroll = scrollMax <= 0 ? 0 : clamp(scroll, 0, scrollMax);
		scrollBar.disabled = scrollMax <= 0;
		
		if(doScrollEnd) scroll = scrollMax;
		scrollBar.barSize = (size.y - scrollMax);
		if(mechlab.cursor.draggingElement != scrollBar)
		{
			scrollBar.Draw(dT);
			double scrollPerc = scroll / scrollMax;
			double toScrollY = pos.y + ( (size.y - scrollBar.size.y) * scrollPerc );
			scrollBar.pos.y = toScrollY;
		}
		else
		{
			scroll = scrollBar.scrollPos * scrollMax;
		}
		
		if(mechlab.cursor.scrollDir > 0) scrollVel += (dT);
		if(mechlab.cursor.scrollDir < 0) scrollVel -= (dT);
		
		scroll += scrollVel;
		scrollVel *= 0.9;
				
		drawCollider();
	}
}
class MechContainerScrollBar : MenuElement
{
	MenuElement parentElement;
	bool disabled;
	double scrollPos;
	double dragYOffs;
	double barSize;
	double maxY;
	
	virtual void Setup(MenuElement parentContainer)
	{
		self.parentElement = parentContainer;
	}
	
	override void OnDrag(MenuCursor cursor)
	{
		dragYOffs = cursor.pos.y - pos.y;
	}
	override bool checkDraggable()
	{
		return !disabled;
	}
	override void updateDragPos(MenuCursor cursor)
	{
		pos.y = cursor.pos.y - dragYOffs;
	}
	
	override void Draw(double dT)
	{
		if(!parentElement) return;
		double minSize = 8;
		barSize = clamp(barSize, minSize, parentElement.size.y);
		size.y  = barSize;
		
		maxY = (parentElement.pos.y+parentElement.size.y) - (size.y);
		pos.y = clamp(pos.y, parentElement.pos.y, maxY);

		double relY = (pos.y-parentElement.pos.y);
		scrollPos = relY / max(1, parentElement.size.y-size.y);
		
		if(!disabled)
		{
			MWDrawer.Fill("Gray", pos.x+4, pos.y+4, size.x, size.y);
			MWDrawer.Fill("White", pos.x , pos.y  , size.x, size.y);
		}
		else
		{
			MWDrawer.Fill("Black", pos.x+4, pos.y+4, size.x, size.y);
			MWDrawer.Fill("Gray", pos.x , pos.y  , size.x, size.y);
		}
		
		DrawCollider();
	}
}

class MechInvItem : MenuElement
{
	MechItem item;
	int itemID;
	bool grayed;
	bool outOfBounds;
	bool dragging;
	vector2 defaultPos;
	
	override void Init()
	{
		super.Init();
		defaultPos = pos;
	}
	
	override bool checkDraggable()
	{
		return !outOfBounds;
	}	
	override void updateDragPos(MenuCursor cursor)
	{
		pos = cursor.pos - (
			(size.x / 2.0) - (cursor.size.x / 2.0), 
			(size.y / 2.0) + 4
		);
		dragging = true;
	}
	
	override void OnDrop(MenuCursor cursor)
	{
		pos = defaultPos;
		dragging = false;
		
		let mouseOverContainer = MechHardpointContainer(cursor.getCollidingWithType("MechHardpointContainer"));		
		if(mouseOverContainer)
		{
			// Equip
			EventHandler.SendNetworkEvent(String.Format("mechlab.item.equip.%d.%d", itemID, mouseOverContainer.partID));
		}
	}
	
	override void Draw(double dT)
	{
		Font itemFont = "SmallFont"; 
		
		Color slotColor = 0;
		uint itemFontColor = Font.CR_WHITE;
		[itemFontColor, slotColor] = MechItemSlot.GetItemColors(item);
		if(grayed) 
		{
			itemFontColor = Font.CR_BLACK;
			slotColor = "Gray";
		}
		
		double scale = 0.5;
		double yscale = 1.1;
		double slotHeight = 32 * scale;
		double wScale = size.x / 256.0;
		vector2 amtOffs = (size.x * 0.5, -(size.y*0.25));
		
		size.y = item ? slotHeight * max(1,item.size) * yscale : slotHeight;
		
		MWDrawer.DrawImage("Graphics/Mechlab/itemTop.png", pos, scale:(scale*wScale,scale*yscale), tint:slotColor, texoffs:(0,0));
		
		if(item)
		{
			int itemSize = max(1,item.size);
			for(int i = 0; i < itemSize-1; i++)
			{
				MWDrawer.DrawImage(
					"Graphics/Mechlab/itemBottom.png", 
					pos + (0, slotHeight + (i * slotHeight*yscale)), 
					scale:(scale*wScale,scale*yscale), tint:slotColor, 
					texoffs:(0,0)
				);
			}
			
			// Draw item data
			double fontScale = wScale*1.0;
			vector2 centerPos = pos + (
				(size.x / 2.0),
				(size.y / 2.0) - ( (itemFont.GetHeight() * (2.0*fontScale) ) / 2.0 )
			);
			
			MWDrawer.DrawString(
				itemFont, item.displayName, 
				centerPos, MWDrawer.SS_TEXT_CENTER, itemFontColor,
				scale:(fontScale, fontScale)
			);
			
			// Quantity
			if(!dragging || grayed)
			{
				int itemAmount = grayed ? item.amount - 1: item.amount; 
				MWDrawer.DrawString(
					itemFont, String.Format("%d x ", itemAmount), 
					centerPos - amtOffs, MWDrawer.SS_TEXT_RIGHT, Font.CR_WHITE,
					scale:(fontScale, fontScale)*1.2
				);
			}
		}
	}
}

// Buttons
class MechlabSquareButton : MenuElement
{
	string btnChar;
	bool btnDown;
	bool pressed;
	bool disabled;
	double heldTime;
	
	virtual void Setup(string btnChar = "+")
	{
		self.btnChar = btnChar;
	}

	override void Draw(double dT)
	{
		let cursor = MenuCursor(getCollidingWithType("MenuCursor"));
		
		if(btnDown)
			heldTime += dT;
		else
			heldTime = 0;
			
		if(!disabled)
		{
			if(heldTime > 5)
			{
				pressed = true;
				OnPressed();
				heldTime = 0;
			}
		
			if(cursor && btnDown && !cursor.leftDown && !pressed) 
			{
				pressed = true;
				OnPressed();
			}
		}
		btnDown = (cursor ? cursor.leftDown : false);
	
		double txtScale = size.x / 16.;
		if(!btnDown && !disabled)
		{
			MWDrawer.DrawRect(pos, size + (2,2), "DarkGray");
			MWDrawer.Fill("White", pos.x,pos.y, size.x,size.y);
			MWDrawer.DrawString("ConsoleFont", btnChar, pos - (0.5,2), translation:Font.CR_GRAY, scale:(txtScale,txtScale));
		}
		else
		{
			MWDrawer.DrawRect(pos, size + (2,2), "DarkGray");
			MWDrawer.Fill("White", pos.x,pos.y, size.x + 2,size.y + 2);
			MWDrawer.DrawString("ConsoleFont", btnChar, pos - (0,0), translation:Font.CR_BLACK, scale:(txtScale,txtScale));
		}
	}
	
	virtual void OnPressed() {}
}

// Mechlab Menu
class MechMenu_Mechlab : MechMenu 
{
	MechLabCursor cursor;
	DrivableMech mech;
	
	override void InitElements() 
	{
		// Get active mech.
		let drvObj = I_DrivingObject.Get(players[consoleplayer].mo);
		if(drvObj && drvObj.source) mech = DrivableMech(drvObj.source);
		if(!mech) return;
	
		vector2 hpSlotsPos = (750,325);
		double slotsWidth = 196;
		double spacing = 205;
	
		// Head
		let headSlots = MechHardpointContainer(
			AddElement("MechHardpointContainer", "Hardpoint_HD", hpSlotsPos - (0,25), (slotsWidth,256))
		);
		headSlots.Setup("Head", mech.head, slotsWidth, 34);
	
		// Arms
		let leftArmSlots = MechHardpointContainer(
			AddElement("MechHardpointContainer", "Hardpoint_LA", hpSlotsPos - (spacing*2, -50), (slotsWidth,256))
		);
		leftArmSlots.Setup("Left Arm", mech.leftArm, slotsWidth, 34);
		
		let rightArmSlots = MechHardpointContainer(
			AddElement("MechHardpointContainer", "Hardpoint_RA", hpSlotsPos + (spacing*2, 50), (slotsWidth,256))
		);
		rightArmSlots.Setup("Right Arm", mech.rightArm, slotsWidth, 34);
		
		// Torso(s)
		let leftTorsoSlots = MechHardpointContainer(
			AddElement("MechHardpointContainer", "Hardpoint_LT", hpSlotsPos - (spacing,0), (slotsWidth,256))
		);
		leftTorsoSlots.Setup("Left Torso", mech.leftTorso, slotsWidth, 34);
		
		let centerTorsoSlots = MechHardpointContainer(
			AddElement("MechHardpointContainer", "Hardpoint_CT", hpSlotsPos + (0,230), (slotsWidth,256))
		);
		centerTorsoSlots.Setup("Center Torso", mech.torso, slotsWidth, 34);
		
		let rightTorsoSlots = MechHardpointContainer(
			AddElement("MechHardpointContainer", "Hardpoint_RT", hpSlotsPos + (spacing,0), (slotsWidth,256))
		);
		rightTorsoSlots.Setup("Right Torso", mech.rightTorso, slotsWidth, 34);
		
		// Legs
		let leftLegSlots = MechHardpointContainer(
			AddElement("MechHardpointContainer", "Hardpoint_LL", hpSlotsPos + (-spacing, 355), (slotsWidth,256))
		);
		leftLegSlots.Setup("Left Leg", mech.leftLeg, slotsWidth, 34);
		
		let rightLegSlots = MechHardpointContainer(
			AddElement("MechHardpointContainer", "Hardpoint_RL", hpSlotsPos + (spacing,355), (slotsWidth,256))
		);
		rightLegSlots.Setup("Right Leg", mech.rightLeg, slotsWidth, 34);
		
		// Items inventory
		AddElement("MenuItemsInvContainer", "InventoryContainer", (1380,170), (480,800));
		
		// Cursor is added last, to keep always on-top.
		cursor = MechLabCursor(AddElement("MechLabCursor", "Cursor", (0,0), (10,10))); 
		
	}
	
	override void processNetworkEvent(ConsoleEvent e)
	{
		// Used as a callback from NetworkProcess, primarily to keep
		// code functionality organized and more readable.
	}
	
	override void OnElementEvent(MenuElement source, string eventType, int intArg, double doubleArg, Object objArg)
	{
		// MenuElements can call this function if they wish to relay behavior to their parent menu.
	}
}
