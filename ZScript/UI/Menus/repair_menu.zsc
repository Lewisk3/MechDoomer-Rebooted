
class PaperDollContainer : MenuElement
{
	MechRepairMenu base;
	HardpointButton selectedHardpoint;
	
	enum PaperDollMode 
	{	
		PDM_ARMOR,
		PDM_INTERNAL
	};
	
	enum ArmorMode 
	{
		AM_FRONT,
		AM_REAR
	};
	
	int paperDollType;
	int paperDollArmor;
	double curRepairCost;
		
	RepairMenuButton btnArmorMode, btnInternalMode;
	RepairMenuButton btnArmorFront, btnArmorRear;
	RepairMenuButton btnDoRepair;

	override void Init()
	{
		base = MechRepairMenu(parentMenu);
		paperDollType = PDM_ARMOR;
		paperDollArmor = AM_FRONT;
		
		Array<MechHardpoint> hardpoints;
		base.mech.getHardpoints(hardpoints, skipDestroyed:false);
		
		foreach(hp : hardpoints)
		{
			let hpButton = HardpointButton(AddChild(base, "HardpointButton", "Hardpoint", pos + (size * 0.5), size));
			hpButton.hp = hp;
		}
		
		vector2 pdmPos = pos + (0, -100);
		btnArmorMode = RepairMenuButton.AddToParent(self, "RepairMenuButton", "Armor", pdmPos, (160,64));
		btnInternalMode = RepairMenuButton.AddToParent(self, "RepairMenuButton", "Internals", pdmPos + (238, 0), (160,64));
		
		vector2 amPos = pos + (0, size.y + 30);
		btnArmorFront = RepairMenuButton.AddToParent(self, "RepairMenuButton", "Front", amPos + (8, 0), (160,64));
		btnArmorRear = RepairMenuButton.AddToParent(self, "RepairMenuButton", "Rear", amPos + (230, 0), (160,64));
		btnDoRepair = RepairMenuButton.AddToParent(self, "RepairMenuButton", "Repair", amPos + (500, 250), (160,64));
	}
	
	override void Draw(double dT)
	{
		// Background
		MWDrawer.Fill(0xFF7F7F7F, pos.x, pos.y, size.x, size.y, 1.0);		
		
		// Paperdoll background
		double scale = size.y / 300.0;
		MWDrawer.DrawImage(base.mech.paperdoll_background, pos + (size * 0.5), scale:(scale,scale)); 
		
		// Button background
		MWDrawer.Fill(0xFF7F7F7F, btnArmorMode.pos.x - 8, btnArmorMode.pos.y - 16, size.x + 16, btnArmorMode.size.y + 32);
		
		MWDrawer.Fill(
			btnArmorFront.active ? 0xFF7F7F7F : 0xFF3F3F3F, 
			btnArmorFront.pos.x - 8, btnArmorFront.pos.y - 30, 
			btnArmorFront.size.x + 16, btnArmorFront.size.y + 48
		);
		MWDrawer.Fill(
			btnArmorRear.active ? 0xFF7F7F7F : 0xFF3F3F3F, 
			btnArmorRear.pos.x - 8, btnArmorRear.pos.y - 30, 
			btnArmorRear.size.x + 16, btnArmorRear.size.y + 48
		);
		
		drawMechInfo(dT);
		drawRepairInfo(dT);
	}
	
	void drawRepairInfo(double dT)
	{
		let selected = selectedHardpoint ? selectedHardpoint.hp : NULL;
		let mech = base.mech;
		
		// Repair button
		btnDoRepair.disabled = (selected == NULL);
		
		vector2 infoPos = pos + (480,-120);
		vector2 infoSize = size + (50,-80);
		Color edgeColor = 0xFF151515;
		Color bgColor = 0xFF7A7A7A;
		btnDoRepair.pos = infoPos + ((infoSize.x * 0.5) - 84,infoSize.y - 96);
		
		// Draw box
		MWDrawer.Fill(edgeColor, infoPos.x-2, infoPos.y-2, infoSize.x+4, infoSize.y+4);
		MWDrawer.Fill(bgColor, infoPos.x, infoPos.y, infoSize.x, infoSize.y);
		
		if(selected)
		{
			string armorRating = "0 / 0";
			string internals = "0 / 0";
			double armorRepairCost, internalRepairCost;
			double repairCost;
			string hpName = "";
			if(selected ==       mech.head) hpName = "Head";
			if(selected ==       mech.torso) hpName = "Center Torso";
			if(selected ==   mech.leftTorso) hpName = "Left Torso";
			if(selected ==  mech.rightTorso) hpName = "Right Torso";
			if(selected ==     mech.leftArm) hpName = "Left Arm";
			if(selected == 	  mech.rightArm) hpName = "Right Arm";
			if(selected == 	   mech.leftLeg) hpName = "Left Leg";
			if(selected == 	  mech.rightLeg) hpName = "Right Leg";
			switch(paperDollArmor)
			{
				case AM_FRONT:
					hpName = String.Format("%s - %s", hpName, "Front");
				break;
				case AM_REAR:
					hpName = String.Format("%s - %s", hpName, "Rear");
				break;
			}
			
			// Draw selected hardpoint name
			MWDrawer.DrawString("hm_type", hpName, infoPos + (infoSize.x * 0.5, 20), MWDrawer.SS_TEXT_CENTER);
			
			[armorRepairCost, internalRepairCost] = selected.getRepairCosts(paperDollArmor == AM_REAR);
			curRepairCost = (paperDollType == PDM_ARMOR ? armorRepairCost : internalRepairCost);
			btnDoRepair.disabled = curRepairCost <= 0;
			
			double finalBalance = base.playerItems.cbills - curRepairCost;
			string balString = MWDrawer.FormatNumber(finalBalance);
			balString = String.Format("\c[%s]%s", (finalBalance < 0 ? "Red" : "Green"), balString);
			
			if(mech.armorType)
			{
				int curArmor = selected.frontArmor;
				int maxArmor = selected.maxFrontArmor;
				if(paperDollArmor == AM_REAR)
				{
					curArmor = selected.rearArmor;
					maxArmor = selected.maxRearArmor;
				}
				
				armorRating = String.Format("%d / %d", curArmor, maxArmor);
			}
			if(mech.chassisStructure)
			{
				int curInternals = selected.healthPoints;
				int maxInternals = selected.maxHP;
				internals = String.Format("%d / %d", curInternals, maxInternals);
			}
		
			string armorCol = (paperDollType == PDM_ARMOR ? "\c[Blue]" : "\c[White]");
			string internalsCol = (paperDollType == PDM_INTERNAL ? "\c[Blue]" : "\c[White]");
			DrawMechStat(2, armorCol .. "Armor", armorCol .. armorRating, infoPos, infoSize);
			DrawMechStat(3, internalsCol .. "Internals", internalsCol .. internals, infoPos, infoSize);
			DrawMechStat(5, "Repair Cost", MWDrawer.FormatNumber(curRepairCost), infoPos, infoSize);
			DrawMechStat(7, "Final Balance", balString, infoPos, infoSize);
		}
		else
		{
			DrawMechStat(4, "No Hardpoint Selected", " ", infoPos + (20,0), infoSize);
		}
	}
	
	void drawMechInfo(double dT)
	{
		let mech = base.mech;
		vector2 infoPos = pos - (550,120);
		vector2 infoSize = size + (50,-50);

		Color bgColor = 0xFF151515;
		Color edgeColor = 0xFF7A7A7A;
		MWDrawer.Fill(edgeColor, infoPos.x-2, infoPos.y-2, infoSize.x+4, infoSize.y+4);
		MWDrawer.Fill(bgColor, infoPos.x, infoPos.y, infoSize.x, infoSize.y);
		MWDrawer.Fill("Blue", infoPos.x+1, infoPos.y+1, infoSize.x-2, 40);
		MWDrawer.Fill("DarkGray", infoPos.x, infoPos.y+42, infoSize.x, 24);
		
		string mechName = mech.mechChassisName;
		string mechVariant = mech.mechVariantName;
		string armorType = "Unknown";
		string structureType = "Unknown";
		string internals = "Unknown";
		string armorRating = "0 / 0";
		string armorWeight = "0";
		string internalsWeight = "0";
		string armorDamage = "0";
		string internalDamage = "0";
		double armorRepairCost;
		double internalRepairCost;
		string cbills = MWDrawer.FormatNumber(base.playerItems.cbills);
		
		if(mech.armorType) armorType = mech.armorType.displayName;
		if(mech.chassisStructure) structureType = mech.chassisStructure.displayName;
		
		[armorRepairCost, internalRepairCost] = mech.getAllRepairCosts();
		
		string mechTonnage = String.Format(
			"\c[%s] %.2f \c[White]/ %.1f", 
			(mech.maxWeight-mech.weight) < 1 ? "Red" : "White", 
			mech.weight, mech.maxWeight
		);
		
		if(mech.armorType)
		{
			int curArmor = mech.armorType.getArmorPoints(mech);
			int maxArmor = mech.armorType.getMaxArmorPoints(mech);
			armorRating = String.Format("%d / %d", curArmor, maxArmor);
			armorWeight = String.Format("%.2f tons", curArmor * mech.armorType.armorWeight);
			armorDamage = String.Format("%.2f", mech.getArmorDamage());
		}
		if(mech.chassisStructure)
		{
			int curInternals = mech.chassisStructure.getInternals(mech);
			int maxInternals = mech.chassisStructure.getMaxInternals(mech);
			internals = String.Format("%d / %d", curInternals, maxInternals);
			internalsWeight = String.Format("%.2f tons", mech.chassisStructure.GetChassisWeight());
			internalDamage = String.Format("%.2f", mech.getInternalDamage());
		}
		
		MWDrawer.DrawString("hm_type", mechName, infoPos + (10,5));
		MWDrawer.DrawString("hm_type", String.Format("Variant - %s", mechVariant), infoPos + (10,40), scale:(0.8,0.8));
		
		DrawMechStat(1, "Tonnage", mechTonnage, infoPos, infoSize);
		DrawMechStat(2, "Armor Weight", armorWeight, infoPos, infoSize);
		DrawMechStat(3, "Chassis Weight", internalsWeight, infoPos, infoSize);
		DrawMechStat(5, "Armor Damage", armorDamage, infoPos, infoSize);
		DrawMechStat(6, "Internal Damage", internalDamage, infoPos, infoSize);
		DrawMechStat(7, "Armor Cost", String.Format("%d CB", armorRepairCost), infoPos, infoSize);
		DrawMechStat(8, "Internal Cost", String.Format("%d CB", internalRepairCost), infoPos, infoSize);
		DrawMechStat(10, "Armor Type", armorType, infoPos, infoSize);
		DrawMechStat(11, "Structure Type", structureType, infoPos, infoSize);
		DrawMechStat(14, "Balance", cbills .. " CB", infoPos, infoSize);
	}
	
	void DrawMechStat(int lnPos, string stat, string value, vector2 infoBoxPos, vector2 infoBoxSize)
	{	
		vector2 statPos = infoBoxPos + (10,100);
		vector2 valuePos = infoBoxPos + (infoBoxSize.x - 20, 105);
		vector2 offs = (0, 30 * (lnPos-1));
		
		MWDrawer.DrawString("hm_type", stat, statPos + offs);
		MWDrawer.DrawString("hm_thin", value, valuePos + offs, MWDrawer.SS_TEXT_RIGHT);
	}
	
	override void Update(double dT)
	{
		if(btnArmorMode.pressed)
		{
			paperDollType = PDM_ARMOR;
			btnArmorMode.pressed = false;
			MenuSound("Menus/Button/Pressed");
		}
		if(btnInternalMode.pressed)
		{
			paperDollType = PDM_INTERNAL;
			btnInternalMode.pressed = false;
			MenuSound("Menus/Button/Pressed");
		}
		if(btnArmorFront.pressed)
		{
			paperDollArmor = AM_FRONT;
			btnArmorFront.pressed = false;
			selectedHardpoint = NULL;
			MenuSound("Menus/Button/Pressed");
		}
		if(btnArmorRear.pressed)
		{
			paperDollArmor = AM_REAR;
			btnArmorRear.pressed = false;
			selectedHardpoint = NULL;
			MenuSound("Menus/Button/Pressed");
		}
		
		if(btnDoRepair.pressed && selectedHardpoint)
		{
			// Send repair request
			// repair:cost:hardpointID:armor/internal:front/rear
			string repairString = String.Format(
				"repair:%d:%f:%i:%s:%s",
				base.playerMechs.mechsData[base.mech.saveID-1].mechbayID,
				curRepairCost, base.mech.parts.Find(selectedHardpoint.hp),
				(paperDollType == PDM_ARMOR) ? "armor" : "internal",
				(paperDollArmor ==  AM_REAR) ? "rear" : "front"
			);
			EventHandler.SendNetworkEvent(repairString);
			btnDoRepair.pressed = false;
			MenuSound("Menus/Repair/Pressed");
		}
		
		btnArmorMode.active = (paperDollType == PDM_ARMOR);
		btnInternalMode.active = (paperDollType == PDM_INTERNAL);
		btnArmorFront.active = (paperDollArmor == AM_FRONT);
		btnArmorRear.active = (paperDollArmor == AM_REAR);
	}
}

class HardpointButton : MenuElement
{
	MechHardpoint hp;
	MechRepairMenu base;
	PaperDollContainer parent;
	

	override void Init()
	{
		base = MechRepairMenu(parentMenu);
		parent = PaperDollContainer(parentElement);
	}
	
	static string getGraphicOutline(string graphic)
	{
		int ext = graphic.IndexOf(".");
		return graphic.Mid(0, ext) .. "_outline" .. graphic.Mid(ext);
	}
	
	override void OnCollision(MenuElement element)
	{
		let cursor = MenuCursor(element);
		if(!cursor) return;
		
		bool rearArmor = parent.paperDollArmor == parent.AM_REAR;
		if(rearArmor && !hp.hasRearArmor) return;
		
		if(cursor && cursor.leftPressed)
		{
			if(parent.selectedHardpoint == self)
				parent.selectedHardpoint = NULL;
			else
				parent.selectedHardpoint = self;
				
			cursor.leftPressed = false;
			MenuSound("Menus/Hardpoint/Selected");
		}
	}
	
	override void Draw(double dT)
	{
		double scale = parentElement.size.y / 300.0;
		
		bool hovered = !!getCollidingWithType("MenuCursor");
		bool selected = parent.selectedHardpoint == self;
		bool internalMode = parent.paperDollType == parent.PDM_INTERNAL;
		bool rearArmor = parent.paperDollArmor == parent.AM_REAR;
	
		string baseGraphic = "Graphics/HUD/PaperDoll/";
		string hpArmor =  baseGraphic .. hp.paperdoll_ArmorGraphic;
		string hpInternals = baseGraphic .. hp.paperdoll_InternalGraphic;
		string hpArmor_Outline = getGraphicOutline(hpArmor);
		
		if(rearArmor && !hp.hasRearArmor) return;
		
		double curArmor, curInternals;
		[curArmor, curInternals] = hp.getVitals();
		if(rearArmor) curArmor = hp.getRearVitals();
		
		Color armorCol = hp.getHealthyColor(max(0.01, curArmor));
		Color internalCol = hp.getHealthyColor(max(0.01, curInternals));
		
		MWDrawer.DrawImage(
			hpArmor, 
			parentElement.pos + (parentElement.size * 0.5), 
			alpha: internalMode ? 0 : 1.0,
			scale:(scale,scale), 
			tint:armorCol
		);
		
		MWDrawer.DrawImage(
			hpInternals, 
			parentElement.pos + (parentElement.size * 0.5), 
			alpha: internalMode ? 1.0 : 0,
			scale:(scale,scale), 
			tint:internalCol
		);
		
		if(selected)
		{
			Color hpTint = 0xE70000FF;
			MWDrawer.DrawImage(
				hpArmor, 
				parentElement.pos + (parentElement.size * 0.5), 
				alpha:0.5, 
				scale:(scale,scale), 
				tint:hpTint
			);
		}
		
		if(hovered || selected) MWDrawer.DrawImage(
			hpArmor_Outline, 
			parentElement.pos + (parentElement.size * 0.5), 
			scale:(scale,scale), 
			tint:0xFF0000FF
		);
		
		// Adjust pos and size
		vector2 basePos = parentElement.pos;
		pos = (0,0);
		size = (0,0);
		
		if(hp == base.mech.leftArm)
		{
			pos = (15,65);
			size = (45,90);
		}
		if(hp == base.mech.rightArm)
		{
			pos = (15 + 120,65);
			size = (45,90);
		}
		if(hp == base.mech.leftTorso)
		{
			pos = (65,55);
			size = (20,65);
		}
		if(hp == base.mech.rightTorso)
		{
			pos = (65 + 49,55);
			size = (20,65);
		}
		if(hp == base.mech.torso)
		{
			pos = (85,85);
			size = (30,50);
		}
		if(hp == base.mech.leftLeg)
		{
			pos = (30,160);
			size = (55,100);
		}
		if(hp == base.mech.rightLeg)
		{
			pos = (30 + 83,160);
			size = (55,100);
		}
		if(hp == base.mech.head)
		{
			pos = (89,56);
			size = (21,20);
		}
		pos *= scale;
		size *= scale;
		pos += basePos;
	}
}

class RepairMenuButton : MenuButton
{
	bool active;
	string text;

	static RepairMenuButton AddToParent(MenuElement parent, class<RepairMenuButton> cls, string txt, vector2 pos, vector2 size)
	{
		let btn = RepairMenuButton(parent.AddChild(parent.parentMenu, cls, txt, pos, size));
		if(btn)
		{
			btn.text = txt;
		}
		return btn;
	}
	
	override void Draw(double dT)
	{
		Font fnt = "hm_type";
		double txtScale = 0.8;
		vector2 txtPos = pos + (size * 0.5);
		double textHeight = fnt.getHeight() * (1/txtScale);
		
		Color bgColor = hovered ? 0xA3092f9e : 0xFFa1a1a1;
		if(active || disabled) bgColor = 0xFF49494a;
		
		if(!hovered && !active) MWDrawer.Fill(0xFF1F1F1F, pos.x + 2, pos.y + 2, size.x + 2, size.y + 2);
		MWDrawer.Fill(bgColor, pos.x, pos.y, size.x, size.y);
		
		MWDrawer.DrawString(fnt, text, txtPos - (0, textHeight * 0.75), MWDrawer.SS_TEXT_CENTER, scale:(txtScale,txtScale));
	}
}


// Mech repair Menu
class MechRepairMenu : MechMenu 
{
	PlayerPawn plr;
	DrivableMech mech;
	MenuCursor cursor;
	
	I_MechItemStorage playerItems;
	I_SaveMechData playerMechs;
	PaperDollContainer paperDoll;
	

	override void InitElements() 
	{
		elements.Clear(); // Repopulate elements
		
		// Cursor is added last, to keep always on-top.
		cursor = MenuCursor(AddElement("MenuCursor", "Cursor", (0,0), (10,10))); 
	}
	
	void PopulateMechElements()
	{
		playerItems = I_MechItemStorage(plr.FindInventory("I_MechItemStorage"));
		playerMechs = I_SaveMechData(plr.FindInventory("I_SaveMechData"));
		paperDoll = PaperDollContainer(AddElement("PaperDollContainer", "PaperDoll", (750,250), (400,600)));
	}
	
	
	play static void processRepairMenuEvents(ConsoleEvent e)
	{
		// Menu events
		let plr = players[e.Player].mo;
		let playerMechs = I_SaveMechData(plr.FindInventory("I_SaveMechData"));
		let playerItems = I_MechItemStorage(plr.FindInventory("I_MechItemStorage"));
		if(!playerMechs || !playerItems) return;
		
		string menuCmd = e.Name;
		Array<String> menuArgs;
		menuCmd.Split(menuArgs, ":");
		
		if(menuArgs[0] ~== "repair")
		{
			int mechbayID = menuArgs[1].toInt();
			double cost = menuArgs[2].toDouble();
			int partID = menuArgs[3].toInt();
			string type = menuArgs[4];
			string face = menuArgs[5];
			
			if( (playerItems.cbills - cost) < 0 ) return;
			
			let mech = playerMechs.getMechbayMech(mechbayID);
			let hp = MechHardpoint(mech.parts[partID]);
			
			if(!hp)
			{	
				console.printf("Failed to find hardpoint for repair.");
				return;
			}
			
			if(type ~== "armor")
				hp.fullRepairArmor(face ~== "rear");
			else
				hp.fullRepairInternals();
				
			playerItems.cbills -= cost;
		}
	}
	
	override void processPlayEvent(int playerNumber, string event) 
	{
		Array<String> cmds;
		event.Split(cmds, ".");
		
		let plr = players[playerNumber].mo;
		if(cmds[0] == "repairmenu")
		{		
			if(cmds[1] == 'update')
			{
				let mechEvents = MechInteraction_Handler(EventHandler.Find("MechInteraction_Handler"));
				if(!mechEvents) return;
			
				self.plr = plr;
				mech = mechEvents.mechlab_curmech[playerNumber];
				PopulateMechElements();
			}
		}
	}
}