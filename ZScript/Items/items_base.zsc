class MechItem : Object
{
	MechHardpoint parent;
	DrivableMech mech;
	double hp;
	int size;
	bool movable, damagable, disabled, nosave; // No Save means this item is not saved into mech data.
	string displayName;
	uint amount;
	double weight;
	
	enum ItemActionError 
	{
		IAE_FAIL 		= 0,
		IAE_NOSLOTS 	= 1,
		IAE_OVERWEIGHT  = 2,
		IAE_INVALID 	= 3,
		IAE_EXCLUSIVE	= 4,
		IAE_SUCCESS 	= 0xFF
	};
	
	clearscope static MechItem Init(class<MechItem> type)
	{
		let item = MechItem(new(type));
		if(item) 
		{
			item.Movable = true;
			item.hp = 10;
			item.size = 1; 
			item.amount = 1;
			item.damagable = false;
			item.Defaults();
		}
		return item;
	}
	
	virtual string GetItemErrorMessage(int ERR_TYPE)	
	{
		switch(ERR_TYPE)
		{
			case IAE_FAIL	   : return String.Format("Failed to equip item.");
			case IAE_NOSLOTS   : return String.Format("Not enough slots!");
			case IAE_OVERWEIGHT: return String.Format("Overtonnage!");
			case IAE_INVALID   : return String.Format("This item cannot be equipped here.");
			case IAE_EXCLUSIVE : return String.Format("Only one of these items may be equipped.");
			
			// No Error message.
			case IAE_SUCCESS:
			default: return "";
		}
	}
	
	
	// Called to check if new item should be added to `amount` or physically differentiated.
	virtual bool CanStack(MechItem other)
	{
		return (other.getClass() == getClass()) && (other.hp == hp && movable);
	}
	
	// Called when item is added to inventory (Not attached to a mech)
	virtual void OnInventoryAdd() {}
	
	clearscope virtual void getDescription(out string flavorText, out Array<String> statName, out Array<String> statValue, DrivableMech mechInfo = NULL)
	{
		if(checkDestroyed())
		{
			flavorText = "\c[Red]DESTROYED\c[White]";
			//return;
		}
		else
		{
			flavorText = "";
		}
		
		statName.Push("SLOTS");
		statName.Push("TONS");
		statValue.Push(String.Format("%d", size));
		statValue.Push(String.Format("%.2f", weight));
		
		if(damagable)
		{
			statName.Push("HEALTH");
			statValue.Push(String.Format("%d / %d", hp, 10));
		}
	}
	
	virtual MechItem MakeCopy()
	{
		let item = MechItem.Init(getClass());
		item.hp = hp;
		return item;
	}
	
	play virtual int TryAttach(MechHardpoint part)
	{
		if(part.mech.noItemSlots) return false;
		if (size > 0)
		{
			if( (part.slots + size) > part.maxslots ) return IAE_NOSLOTS;
			if( (part.mech.weight + weight) > part.mech.maxWeight ) 
				return IAE_OVERWEIGHT;	
				
			int freeSlots = MechDynamicStructure.getTotalFreeslots(part.mech);
			if( (freeSlots - size) < 0 ) return IAE_NOSLOTS;
		}
		
		parent = part;
		part.items.Push(self);
		OnAttach();
		
		part.slots += size;
		part.mech.weight += weight;
		return IAE_SUCCESS;
	}
	
	play virtual bool TryDetach(bool forced = false)
	{
		if(!movable && !forced || !parent) return false;
		uint id = parent.items.Find(self);
		OnDetach();
		
		parent.items.Delete(id);
		parent.slots -= size;
		parent.mech.weight -= weight;
		parent = NULL;
		
		return true;
	}
	
	clearscope virtual void Defaults()
	{
		// Used to set defaults as we cannot otherwise.
	}
	
	play virtual void OnAttach()
	{	
		mech = parent.GetMech();
	}
	
	play virtual void OnDetach()
	{
		// When detached from parent.
	}
	
	clearscope virtual string GetDesc()
	{
		return "Unknown Item";
	}
	
	play virtual void OnDamaged(double dmg)
	{
		if(!damagable) return;
	
		// Do stuff when damaged.
		hp -= dmg;
		if(hp <= 0) OnDestroyed();
	}
	
	play virtual void OnDestroyed()
	{
		// Do stuff when destroyed.
		mech.betty.Push(String.Format("Betty/ItemDestroyed/%s", getClassName()));
	}
	
	virtual bool checkDestroyed()
	{
		if(!parent) return hp <= 0;
		return hp <= 0 || parent.healthPoints <= 0;
	}
	
	play virtual void modifyHeat(double amt)
	{
		mech.modifyHeat(amt);
		parent.adjustLocalHeat(amt);
	}
	
	play virtual void DoEffect()
	{
		// Under normal circumstances, if our hardpoint was destroyed, we took a lot of damage too.
		if(parent.healthPoints <= 0) hp = 0;
	
		if(!mech) mech = parent.getMech();
		
		// Process destruction.
		if(!parent) return;
	}
}

class MechAmmo : MechItem
{
	int curAmmo;
	int maxAmmo;
	
	override void Defaults()
	{
		damagable = true;
	}
	
	virtual void SetupAmmo()
	{
		curAmmo = maxAmmo;
	}
	
	override void OnDestroyed()
	{
		if(curAmmo <= 0) return;
		mech.betty.Push("Betty/AmmoExplosion");
		parent.DoExplosionDamage(curAmmo);
		curAmmo = 0;
	}
	
	override bool CanStack(MechItem other)
	{
		bool res = super.CanStack(other);
		return res && MechAmmo(other).curAmmo == curAmmo;
	}
	
	override void getDescription(out string flavorText, out Array<String> statName, out Array<String> statValue, DrivableMech mechInfo)
	{
		super.getDescription(flavorText, statName, statValue);
		statName.Push(" ");
		statValue.Push(" ");
		
		statName.Push("AMMO");
		statValue.Push(String.Format("%d / %d", curAmmo, maxAmmo));
	}
	
	override MechItem MakeCopy()
	{
		let item = MechAmmo(super.MakeCopy());
		item.curAmmo = curAmmo;
		item.maxAmmo = maxAmmo;
		return item;
	}
	
	override void DoEffect()
	{
		super.DoEffect();
		
		// Discard when depleted.
		if(curAmmo <= 0) 
			TryDetach(forced:true);
	}
}

class MechDynamicStructure : MechItem
{
	override void Defaults()
	{
		movable = false;
		weight = 0;
		size = 0;
		nosave = true;
		displayName = "Structure";
	}
	
	clearscope virtual int getFreeslots()
	{
		int curSlots = parent.slots;
		int maxSlots = parent.maxSlots;
		curSlots += parent.countItems("MechDynamicStructure", true);
		
		return (maxSlots-curSlots);
	}
	
	clearscope static int getTotalFreeslots(DrivableMech mech)
	{
		int maxSlots;
		int curSlots;
		Array<MechHardpoint> hardpoints;
		mech.getHardPoints(hardpoints);
		
		for(int i = 0; i < hardpoints.Size(); i++)
		{
			let hardpoint = hardpoints[i];
					
			for(int x = 0; x < hardpoint.items.Size(); x++)
			{
				let item = hardpoint.items[x];
				if(item is "MechDynamicStructure") continue;
				curSlots += item.size;
			}
			
			curSlots += hardpoint.countItems("MechDynamicStructure", true);
			maxSlots += hardpoint.maxSlots;
		}
		
		return (maxSlots-curSlots);
	}
	
	clearscope static MechHardpoint findFreeslots(DrivableMech mech)
	{
		Array<MechHardpoint> hardpoints;
		mech.getHardPoints(hardpoints);
		
		for(int i = 0; i < hardpoints.Size(); i++)
		{
			let hardpoint = hardpoints[i];
			uint curSlots = 0;
			
			for(int x = 0; x < hardpoint.items.Size(); x++)
			{
				let item = hardpoint.items[x];
				if(item is "MechDynamicStructure") continue;
				curSlots += item.size;
			}
			curSlots += hardpoint.countItems("MechDynamicStructure", true);
			
			if(curSlots < hardpoint.maxSlots)
				return hardpoint;
		}
		
		return NULL;
	}
	
	play virtual void ReAttach()
	{
		let freeHardpoint = findFreeslots(mech);
		if(freeHardpoint)
		{
			TryDetach(true);
			TryAttach(freeHardpoint);
		}
	}
	
	override void DoEffect()
	{
		super.DoEffect();
		
		if(parent.healthPoints <= 0) return; // Do not move if we're in a dead hardpoint.
		if( (getFreeslots() <= 1 && !findFreeslots(mech)) && size < 1 )
		{
			size = 1;
			parent.slots++;
		} 
		
		if(findFreeslots(mech) && size > 0)
		{
			size = 0;
			parent.slots--;
		}
		
		if(getFreeslots() < 0 || findFreeslots(mech) != parent)
		{
			ReAttach();
		}
	}
}

class MechArmor : MechItem
{
	double armorWeight;
	double armorAmount;
	double damageMod;
	class<MechDynamicStructure> structType;
	int structureSlots;

	override void Defaults()
	{
		hp = 100;
		size = 0; 
		structType = "MechDynamicStructure";
		structureSlots = 0;
		armorWeight = 0.05;
	}
	
	override void getDescription(out string flavorText, out Array<String> statName, out Array<String> statValue, DrivableMech mechInfo)
	{
		statName.Push("ARMOR WEIGHT");
		statValue.Push(String.Format("%.2f", armorWeight));
		
		statName.Push("CRITICALS");
		statValue.Push(String.Format("%d", structureSlots));
		
		if(damageMod < 1.0)
		{
			statName.Push("ABSORBTION");
			statValue.Push(String.Format("%d\%", 1.0-damageMod));
		}
	}
	
	play static bool addToMech(DrivableMech mech, class<MechArmor> type = "MechArmor")
	{
		let ar = MechItem.Init(type);
		if(ar) return ar.TryAttach(mech.torso) == IAE_SUCCESS;
		return false;
	}
	
	virtual double ProcessDamage(double amt)
	{
		return amt * damageMod;
	}
	
	clearscope static double getMaxArmorPoints(DrivableMech mech)
	{
		double maxArmorPts = 0;
		Array<MechHardpoint> hardpoints;
		mech.getHardPoints(hardpoints);
		
		for(int i = 0; i < hardpoints.Size(); i++)
			maxArmorPts += hardpoints[i].maxArmor;
		
		return maxArmorPts;
	}
	
	clearscope static double getArmorPoints(DrivableMech mech)
	{
		double fullArmorPts = 0;
		Array<MechHardpoint> hardpoints;
		mech.getHardPoints(hardpoints);
		
		for(int i = 0; i < hardpoints.Size(); i++)
		{
			let hardpoint = hardpoints[i];
			fullArmorPts += (hardpoint.armorPoints + hardpoint.rearArmor);
		}
		return fullArmorPts;
	}
	
	override void DoEffect()
	{
		// Keep armor weight stable.
		double fullArmorPts = getArmorPoints(mech);
		if(armorAmount != fullArmorPts) 
		{
			mech.weight -= (armorAmount * armorWeight);
			armorAmount = fullArmorPts;	
			mech.weight += (armorAmount * armorWeight);
		}
	}
	
	override int TryAttach(MechHardpoint part)
	{
		let mech = part.mech;
		if(!mech) return IAE_FAIL;
		if(mech.noItemSlots) return IAE_NOSLOTS;
		self.mech = part.mech;
		
		let prevArmor = mech.armorType;
		mech.armorType = NULL;
		if(prevArmor) prevArmor.OnDetach();
		
		int fail = IAE_SUCCESS;
		double weight = armorWeight*getArmorPoints(mech);
		if( (mech.weight + weight) > mech.maxWeight ) fail = IAE_OVERWEIGHT; 
		if(structureSlots > 0 && MechDynamicStructure.getTotalFreeslots(mech) < structureSlots)
			fail = IAE_NOSLOTS;
			
		if(fail != IAE_SUCCESS)
		{
			prevArmor.TryAttach(part);
		}
		else
		{
			mech.armorType = self;
			OnAttach();
		}
		return fail;
	}
	
	override bool TryDetach(bool forced)
	{
		OnDetach();
		return true;
	}
	
	override void OnAttach()
	{
		DoEffect(); // Setup tonnage.
		for(int i = 0; i < structureSlots; i++)
			mech.torso.AddItem(structType);
	}
		
	override void OnDetach()
	{
		// Remove armor weight from mech.
		mech.weight -= (armorAmount * armorWeight);
		armorAmount = 0;

		MechItem structSlot;
		while(structSlot = mech.findItem(structType))
			structSlot.TryDetach(forced:true);
	}
}

class MechEngineBlock : MechItem
{
	override void Defaults()
	{
		movable = false;
		weight = 0;
		size = 2;
		nosave = true; 
		displayName = "??? ENGINE";
	}
}

class MechEngine : MechItem
{
	double baseHeatEff;
	int rating, type;
	uint blocks, blockSize; // How many engine blocks to add to both side torsos.
	Array<MechItem> engineBlocks;
	Array<MechHeatsink> heatSinks;
	
	enum EngineType 
	{
		Type_STD,
		Type_Light,
		Type_XL,
		Type_XXL
	}
	
	double roundTons(double value)
	{	
		return ceil(value*2.0)/2.0;
	}
	
	override void getDescription(out string flavorText, out Array<String> statName, out Array<String> statValue, DrivableMech mechInfo)
	{
		super.getDescription(flavorText, statName, statValue);
		statName.Push(" ");
		statValue.Push(" ");
		
		// Modify slots to reflect side slots
		int slotsIndex = statName.Find("SLOTS");
		statValue[slotsIndex] = String.Format("%d", size + (blocks*2) * blockSize);
		
		statName.Push("SIDE SLOTS");
		statValue.Push(String.Format("%d",blocks*blockSize));
		
		statName.Push("HEATSINKS");
		statValue.Push(String.Format("%d",floor(rating*0.04)));
		
		if(mechInfo)
		{
			statName.Push("TOP SPEED");
			statValue.Push(String.Format("%.2f KPH", getKPH(mechInfo)));
		}
	}
	
	double GetTonnage()
	{
		double std_index_tons[99] =
		{
			0.5,0.5,0.5,0.5,1.0,1.0,1.0,1.0,
			1.5,1.5,1.5,2.0,2.0,2.0,
			2.5,2.5,3.0,3.0,3.0,
			3.5,3.5,4.0,4.0,4.0,
			4.5,4.5,5.0,5.0,
			5.5,5.5,6.0,6.0,6.0,
			7.0,7.0,7.5,7.5,8.0,
			8.5,8.5,9.0,
			9.5,10.0,10.0,
			10.5,11.0,
			11.5,12.0,
			12.5,13.0,
			13.5,14.0,
			14.5,15.5,
			16.0,16.5,
			17.5,18.0,
			19.0,19.5,20.5,
			21.5,22.5,
			23.5,24.5,
			25.5,27.0,
			28.5,29.5,
			31.5,33.0,
			34.5,36.5,
			38.5,41.0,
			43.5,46.0,
			49.0,52.5,
			56.5,61.0,66.5,72.5,79.5,97.0,
			107.5,119.5,133.5,150.0,168.5,190.0,
			214.5,243.0,313.0,356.0,405.5,462.5
		};
		double tons = std_index_tons[floor(rating/5)-2];
		if(type == Type_STD    		) return tons;
		if(type == Type_Light  		) return roundTons(tons*0.75);
		if(type == Type_XL	   		) return roundTons(tons*0.50);
		if(type == Type_XXL    		) return roundTons(tons*0.33);
		return tons;
	}
	
	virtual string getTypeStr()
	{
		switch(type)
		{
			case Type_STD:     return "STD";
			case Type_Light:   return "LIGHT";
			case Type_XL:      return "XL";
			case Type_XXL:     return "XXL";	
			default: return "STD";
		}
		
	}
	
	clearscope double getKPH(DrivableMech mech)
	{
		double baserating = 187.5; 
		return (32.4*(100./mech.maxWeight)) / (baserating/rating); 
	}
		
	clearscope static double speedToKPH(double spd, double scalar = 1.0)
	{
		double unitsPerSecond = spd * TICRATE;
		double unitsPerHour = unitsPerSecond * 3600.0;
		return scalar * (( unitsPerHour * (UTOCM / level.pixelStretch) ) / 100000.0);
	}
	
	// This is for a Mech, not a projectile.
	clearscope static double getKPHSpeed(double KPH, double scalar = 1.0)
	{
		double unitsPerHour = (KPH*100000.0) / (UTOCM / level.pixelStretch);
		double unitsPerSecond = unitsPerHour / 3600.0;
		
		// Divide by magic constants.
		unitsPerSecond /= (15.66666564814815) * (0.94/DrivableMech.MECHFRICTION);
		unitsPerSecond *= scalar;
		
		return unitsPerSecond / TICRATE;
	}
		
	
	virtual void AutoDetectEngine()
	{
		// Find rating and type based on classname.
		// Naming convention -> MechEngine_ -> STD###, XL###, XXL###, LIGHT###
		
		string clsName = getClassName();
		int engineToken = clsName.IndexOf("_");
		
		string nums = "0123456789";
		int ratingIndex = -1;
		for(uint i = 0; i <= clsName.Length(); i++)
		{
			string ch = String.Format("%c", clsName.ByteAt(i));
			if(nums.IndexOf(ch) != -1)
			{
				ratingIndex = i;
				break;
			}
		}
		
		if(engineToken == -1 || ratingIndex == -1) 
		{
			console.printf("\c[Red]MechEngine Error: failed to autodetect engine parameters for %s.\c[White]", clsName);
			return; // Unrecognized naming convention.
		}
				
		int engineRating = clsName.Mid(ratingIndex).ToInt(); 
		Name engineType = clsName.Mid(engineToken+1, ratingIndex-engineToken-1);
		
		// console.printf("Rating for %s: [%d] %d :> Type is %s", clsName, ratingIndex, engineRating, engineType); 
		
		switch(engineType)
		{
			case   'STD': type = Type_STD;   break;
			case    'XL': type = Type_XL;    break;
			case   'XXL': type = Type_XXL;   break;
			case 'LIGHT': type = Type_Light; break;
		}
		rating = clamp(engineRating, 100, 500);
	}
	
	override void Defaults()
	{	
		if(getClassName() != "MechEngine")
		{
			rating = 100;
			type = Type_STD;
			AutoDetectEngine();
			SetupEngine(automaticParameters:true);
		}
	}
	
	void SetupEngine(bool automaticParameters = true)
	{
		// Setup engine blocks
		switch(type)
		{
			case Type_STD: // 6 + 0
				blocks = blockSize = 0;
			break;
		
			case Type_XL: // 6 + 6
				blocks = 1;
				blockSize = 3;
			break;
			
			case Type_Light: // 6 + 4
				blocks = 1;
				blockSize = 2;
			break;
			
			case Type_XXL: // 6 + 12
				blocks = 1;
				blockSize = 6;
			break;
		}
		
		size = 6;
		nosave = true;
		weight = GetTonnage();
		displayName = String.Format("%s ENGINE %d", getTypeStr(), rating);
	}
	
	override void OnAttach()
	{
		super.OnAttach();
		
		// Add engine blocks.
		let lt = parent.mech.leftTorso;
		let rt = parent.mech.rightTorso;  
		for(uint i = 0; i < blocks; i++) 
		{
			let lt_block = MechItem.Init("MechEngineBlock");
			lt_block.displayName = displayName;
			lt_block.size = blockSize ? blockSize : lt_block.size;
			if(lt_block.TryAttach(lt)) engineBlocks.push(lt_block);
			
			let rt_block = MechItem.Init("MechEngineBlock");
			rt_block.displayName = displayName;
			rt_block.size = blockSize ? blockSize : rt_block.size;
			if(rt_block.TryAttach(rt)) engineBlocks.push(rt_block);
		}
		
		// Add heatsinks to Engine
		int heatsinkAmt = floor(rating*0.04);
		
		for(int i = 0; i < heatsinkAmt; i++)
		{
			let heatsink = MechHeatsink(Init("MechDoubleHeatsink"));
			heatsink.parent = parent;
			heatsink.mech = mech;
			heatsink.OnAttach();
			heatSinks.Push(heatsink);
		}
	}
	
	override void OnDetach()
	{
		super.OnDetach();
	
		// Remove engine blocks.
		for(int i = 0; i < engineBlocks.Size(); i++)
		{
			let block = engineBlocks[i];
			if(block) block.TryDetach(forced:true);
		}
			
		// Remove heatsinks
		for(int i = 0; i < heatsinks.Size(); i++)
		{
			let heatsink = heatsinks[i];
			heatsink.OnDetach();
			heatsink.Destroy();
		}
		
		engineBlocks.Resize(0);
		heatsinks.Resize(0);
	}
	
	override int TryAttach(MechHardpoint part)
	{
		if(part.mech.FindItem("MechEngine", true))
			return IAE_EXCLUSIVE;

		let engBlock = MechItem.Init("MechEngineBlock");
		int eblockSlots = blocks * (blockSize ? blockSize : engBlock.size);
		engBlock.Destroy();
		
		let lt = part.mech.leftTorso;
		let rt = part.mech.rightTorso;
		if( (lt.slots +  eblockSlots) > lt.maxslots ||
			(rt.slots +  eblockSlots) > rt.maxslots    ) 
		{
			console.printf("Slot error : %d, %d, %d", lt.slots, eblockSlots, lt.maxslots);
			return IAE_NOSLOTS;
		}
		
		return super.TryAttach(part.mech.torso);
	}
	
	override void DoEffect()
	{
		super.DoEffect();
		
		double power = abs(mech.throttle);
		modifyHeat(0.16 * power);
		
		for(int i = 0; i < heatsinks.Size(); i++)
		{
			let heatsink = heatsinks[i];
			if(heatsink) heatsink.DoEffect();
		}
	}
}

class MechWeapon : MechItem
{
	int group;
	int fireTime;
	int cooldown;
	int cooldownTime;
	int ammoUse;
	int refire, refireCount, refireDelay, refireTimer; // Used for weapons which fire multiple times.
	bool firing;
	double minrange, range;
	double damage;
	double heat;
	string mountType;
	Sound fireSound;
	Actor homingTarget;
	HardpointMount mount;
	class<MechAmmo> ammoType;
	class<MechProjectile> projType;
	
	// Convert from spreadsheet data.
	virtual double convertHeat(double amt, double delta)
	{
		return (amt * 6.5) / delta;
	}
		
	virtual double convertDMG(double dmg, double delta)
	{
		return (dmg / MechHardpoint.DMGSCALE) / delta;
	}
	// --------------------------- \\
	
	override MechItem MakeCopy()
	{
		let item = MechWeapon(super.MakeCopy());
		item.group = group;
		return item;
	}
	
	play virtual void A_Recoil(double pitchAmt, double pushBack = 0)
	{
		if( !mech || !MechTorso(mech.torso) ) return;
		
		// If we're on an Arm and this arm has upper actuation capability, arm will compensate for recoil.
		let arm = MechArm(mount.hardpoint);
		if(arm && arm.FindItem("MechUpperArmActuator", true))
		{
			arm.pitchOffs -= pitchAmt * 5;
			pitchAmt *= 0.33;
			pushBack *= 0;
		}
		
		MechTorso(mech.torso).pitchBobVel -= pitchAmt;
		
		if(pushBack)
		{
			pushBack *= cos(mech.angle-mech.torso.angle); // Adjust recoil so it's facing the torso direction.
			mech.vel -= DrivablesMath.VecFromAngles(mech.angle, mech.pitch, pushBack);
		}
	}
	
	virtual void WeapSound(Sound snd, double vol = 1.0)
	{
		if(parent) parent.A_StartSound(snd, CHAN_WEAPON, flags:CHANF_OVERLAP, volume:vol);
	}
	
	clearscope virtual int checkAmmo()
	{
		if(!ammoType) return int.max;
		let am = parent.FindItem(ammoType, false);
		if(!am) am = parent.mech.FindItem(ammoType, false);
		return am ? MechAmmo(am).curAmmo : 0;
	}
	
	clearscope virtual bool canFire()
	{
		bool ammoCheck = ammoUse <= 0 || checkAmmo() >= ammoUse;
		return ammoCheck;
	}
	
	play virtual void depleteAmmo(int amt)
	{
		if(!ammoType) return;
		let am = MechAmmo(parent.FindItem(ammoType, false));
		if(!am) am = MechAmmo(parent.mech.FindItem(ammoType, false));
		
		double prevAmmoCount = mech.countAllAmmo(ammoType);
		double criticalAmmo = ammoUse*3.0;
		
		if(am) am.curAmmo -= amt;
		double ammoCount = mech.countAllAmmo(ammoType);
		
		if(prevAmmoCount > 0 && ammoCount <= 0)
			mech.betty.Push("Betty/Ammo/Depleted");
		else if(prevAmmoCount > criticalAmmo && ammoCount <= criticalAmmo)
			mech.betty.Push("Betty/Ammo/Critical");
	}
	
	play virtual void SetGroup(uint grp, bool onoff)
	{
		uint gBit = 2**(grp-1);
		if(onoff) 
			group |= gBit;
		else
			group &= ~gBit;
	}
	
	override int TryAttach(MechHardpoint part)
	{
		// Attempt to find position to mount weapon to.
		let mnt = part.findMountPoint(mountType);
		if(!mnt) return IAE_INVALID;
		
		int success = super.TryAttach(part);
		if(success == IAE_SUCCESS)
		{
			mnt.mountItem(self);
			mount = mnt;
		}
		
		return success;
	}
	
	override void OnInventoryAdd()
	{
		group = 1; // Reset weapon groupings
	}
	override void OnDetach()
	{
		if(mount) mount.unmountItem(); // Clear mech mount point.
	}
	
	play static void FireWeapons(Array<MechWeapon> weaps, Array<MechWeaponGroup> groups, uint grp = 0, bool instantFire = false)
	{		
		let group = grp > 0 ? groups[grp-1] : NULL;
		bool chainfire = grp > 0 ? groups[grp-1].chainfire : false;
		
		// If weapon was removed from the group, discard reference to it.
		if(group.prevWeapFired && weaps.Find(group.prevWeapFired) == weaps.Size())
			group.prevWeapFired = NULL;
		
		for(int i = 0; i < weaps.Size(); i++)
		{	
			let weap = weaps[i];
			let hardpoint = weap.parent;
			if(!hardpoint || !weap) continue;
			if(weap.checkDestroyed()) continue; // Cannot fire destroyed weapons.
			
			// Fire in chainfire.
			if( chainfire && group ) 
			{
				bool fireWait = !instantFire && group.weapCycle > 0;
				if( (!group.prevWeapFired || group.prevWeapFired == weap) && !fireWait )
				{
					int curIndex = i;
					let nextWeap = weaps[(i+1)%weaps.Size()];
					
					if(group.prevWeapFired)
					{
						weap = nextWeap;
						hardpoint = weap.parent;
						curIndex++;
					}
					// console.printf("Firing (Chainfire): %s on %s (%d)", weap.getClassName(), weap.parent.getClassName(), curIndex);
					
					if( weap.canFire() && weap.cooldown <= 0 && weap.mount && weap.Fire(weap.mount.getPos()) )
					{
						// Alert monsters
						if(weap.mech.driver)
							weap.mech.driver.A_AlertMonsters(1024);
						
						weap.modifyHeat(weap.heat);
						weap.cooldown += (weap.cooldownTime + weap.fireTime);
						group.weapCycle = weap.fireTime;
						group.prevWeapFired = weap;
					}
					
					break;
				}
				continue;
			}
			
			// Fire normally
			if( weap.canFire() && weap.cooldown <= 0 && weap.mount && weap.Fire(weap.mount.getPos()))
			{
				// Alert monsters
				if(weap.mech.driver)
					weap.mech.driver.A_AlertMonsters(1024);
			
				weap.modifyHeat(weap.heat);
				weap.cooldown += (weap.cooldownTime + weap.fireTime);
				group.weapCycle = weap.fireTime;
			}
		}
	}
	
	override void Defaults()
	{
		super.Defaults();
		group = 1;
	}
	
	virtual bool DoRefire()
	{
		refire = refireCount;
		refireTimer = refireDelay;
		return true;
	}
	
	override void DoEffect()
	{
		super.DoEffect();
		if(cooldown) cooldown--;
		
		// Refire logic
		if(--refireTimer <= 0 && refire > 0)
		{
			if(canFire() && Fire(mount.getPos()) )
			{
				refireTimer = refireDelay;
				refire--;
			}
			else
				refire = 0;
		}
		
		// Update target
		if(mech.targetProgress >= 1.0) 
			homingTarget = mech.curTarget; 
		else if(mech.targetProgress <= 0.1 && homingTarget)
			homingTarget = NULL;
	}
	
	// Weapons shouldn't get removed from the game once destroyed.
	override void OnDestroyed()
	{
		// Do not remove item.
	}
		
	// Note: firePos is ALWAYS relative offsets from the parent's position
	// to get absolute positions, use parent.OffsetPos(firePos.x,firePos.y,firePos.z)
	play virtual bool Fire(vector3 firePos)
	{
		if(cooldown) return false;
		// Pew pew pew
		return true;
	}
}

// Info about this weapon grouping.
class MechWeaponGroup : Object
{
	MechWeapon prevWeapFired;
	uint weapCycle; // Timer for weapon cycling.
	bool chainfire;
	
	virtual void DoEffect()
	{
		if(weapCycle) weapCycle--;
	}
}

// Inventory item for storing mechitems
class I_MechItemStorage : Inventory
{
	Array<MechItem> items;
	
	Default
	{
		Inventory.MaxAmount 1;
		+INVENTORY.UNDROPPABLE;
	}
	
	void FindItems(class<MechItem> itemCls, Array<MechItem> result, bool subclass = false)
	{
		for(int i = 0; i < items.Size(); i++)
		{
			let item = items[i];
			if( ((item is itemCls) && subclass) || item.getClass() == itemCls )
				result.Push(item);
		}
	}
	
	MechItem takeItem(int index, bool deplete = true)
	{
		let item = items[index];
		MechItem output = (item.amount > 1) ? item.MakeCopy() : item;	
		if(deplete) depleteItem(item);
		
		return output;
	}	
	
	void depleteItem(MechItem item)
	{	
		if(item.amount > 1)
			item.amount--;
		else
		{
			int index = items.Find(item);
			if(index != items.Size()) items.Delete(index);
		}
	}
	
	void AddItem(MechItem item)
	{
		Array<MechItem> validItems;
		FindItems(item.getClass(), validItems);
		
		for(int i = 0; i < validItems.Size(); i++)
		{
			let stackItem = validItems[i];
			if(stackItem && stackItem.CanStack(item))
			{
				stackItem.amount++;
				return; // Successfully stacked item.
			}
		}
		
		item.OnInventoryAdd();
		items.Push(item);
	}
	
	static I_MechItemStorage Get(Actor from, bool give = true)
	{
		let itm = I_MechItemStorage(from.FindInventory("I_MechItemStorage"));
		if(!itm && give)
		{
			from.GiveInventory("I_MechItemStorage", 1);
			itm = I_MechItemStorage(from.FindInventory("I_MechItemStorage"));
				
			// Debug: give all items
			foreach(cls : AllClasses)
			{
				let item = (class<MechItem>)(cls);
				if(!item) continue;
				
				let itemInstance = MechItem.Init(item);
				if( itemInstance && (!itemInstance.movable || itemInstance.displayName == "") )
				{
					itemInstance.Destroy();
					continue;
				}
								
				// Give 25 of each item.
				for(int i = 0; i < 25; i++)
				{
					itm.AddItem(MechItem.Init(item));
				}
			}
		}
		
		return itm;
	}
}