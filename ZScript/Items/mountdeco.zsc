class MechMountDeco : EffectsActor
{
	vector3 offs;
	int mountIndex;
	MechWeapon src;
	vector3 animOffs;
	
	Property MountOffset : offs;
	
	Default
	{
		Scale 1.5;
		MechMountDeco.MountOffset (0, 0, 0);
	}
	
	static MechMountDeco Attach(MechWeapon src, class<MechMountDeco> type, int mountIndex = 0)
	{
		let mountDeco = MechMountDeco(Spawn(type));
		if(mountDeco)
		{
			mountDeco.src = src;
			mountDeco.mountIndex = mountIndex;
		}
		
		return mountDeco;
	}
	
	virtual void followMountPos()
	{
		if(!src.parent || !src.mount) return;
		vector3 mountPos = src.mount.getOffset(mountIndex) + offs + src.mountDecoOffs + animOffs;

		double sc = src.parent.Scale.X;
		mountPos = src.parent.OffsetPos(mountPos.x * sc, mountPos.y * sc, mountPos.z * sc);
		
		SetOrigin(mountPos, true);
		angle = src.parent.angle; 
		pitch = src.parent.pitch;
		roll = src.parent.roll;
		
		double itemMountScale = src.mountDecoScale;
		if(!itemMountScale) itemMountScale = 1.0;
		
		A_SetScale(default.scale.X * src.parent.scale.X * itemMountScale);
	}
	
	override void Tick()
	{
		super.Tick();
			
		// Determine when this mount needs to go away.
		// We don't need to pass mech to weapons to check if they're equipped.	
		if(!src || !src.isEquipped(null) || src.checkDestroyed())
		{
			Destroy();
			return;
		}
	
		followMountPos();
		animOffs *= 0.95;
	}

	States
	{
		Spawn:
			MODL A -1;
		stop;
	}
}

class LaserMountDeco : MechMountDeco {}
class PPCMountDeco : MechMountDeco {}

class MissileMountDeco : MechMountDeco 
{
	override void Tick()
	{	
		super.Tick();
		if(!src || !src.mount) return;
		
		int curAmmo = src.checkAmmo();
		bool noAmmo = (curAmmo < (mountIndex+1));
		bInvisible = noAmmo;
		
		if(noAmmo) animOffs.x = -4;
		
		if(!src.refire) return;
		
		int fireIndex = (src.refireCount - src.refire) % src.mount.countOffsets();
		
		if(fireIndex == mountIndex)
			animOffs.x -= 2;
	}
}

class MGunMountDeco : MechMountDeco 
{
	double spin, spinAccel;
	int prevAmmo;
	
	override void followMountPos()
	{
		super.followMountPos();
		int curAmmo = src.checkAmmo();
		if(curAmmo != prevAmmo)
		{
			prevAmmo = curAmmo;
			spinAccel += 5;
		}
	}
	
	override void Tick()
	{
		super.Tick();
		
		spin += min(50, spinAccel);
		roll += spin;
		
		spinAccel *= 0.98;
	}	
}