class MechDropship_Handler : StaticEventHandler
{
	bool usingMechlab;
	bool forceDropshipMap;
	double scrfade;
	bool onFirstMap;

	virtual void RestoreActiveMechs()
	{
		// Spawn active mech and put the driver in it.
		for(int i = 0; i < MAXPLAYERS; i++)
		{
			PlayerInfo plrInfo = players[i];
			if(!plrInfo || !playerInGame[i]) continue;
			let plr = players[i].mo;
			
			let mechData = I_SaveMechData(plr.FindInventory("I_SaveMechData"));
			if(mechData && mechData.activeMechID > 0)
			{
				let mech = mechData.loadMechData(mechData.activeMechID, plr.pos);
				if(mech) 
				{
					mech.angle = plr.angle;
					mech.autoTakePilot = plr;
					mech.shutdown = true;
					mech.powerupTimer = 35*2;
				}
			}			
		}	
	}
	
	override void WorldTick()
	{
		Name curMap = level.mapname;
		if(level.maptime >= 2 && forceDropshipMap && !sv_mdr_disabledropship)
		{
			level.ChangeLevel("DROPSHIP", flags:CHANGELEVEL_NOINTERMISSION);
			forceDropshipMap = false;
		}
		
		if(sv_mdr_skipmaploading) scrfade = 0;
		
		if(level.maptime >= 8 && scrfade > 0 && gamestate == GS_LEVEL)
		{	
			scrfade *= 0.97;
			if(scrfade < 0.1) scrfade = 0;
		}
	}
	
	override void WorldLoaded(WorldEvent e)
	{
		let plr = players[consoleplayer].mo;
		let inv = I_MechItemStorage.Get(plr);
		if(!inv) return;
				
		Name curMap = level.mapname;
		
		scrfade = 2.0;
		onFirstMap = curmap != "DROPSHIP" && (inv.dropshipNextMap == "") && sv_mdr_startondropship;
		
		if( onFirstMap )
		{
			// Start the player in a dropship.
			inv.dropshipNextMap = curMap;
			forceDropshipMap = true;
		}

		if(curmap != "DROPSHIP")
		{
			inv.dropshipNextMap = level.NextMap;
			if(onFirstMap) inv.dropshipNextMap = curMap;
			
			level.NextMap = "DROPSHIP";		
			
			// If there are active mechs, destroy them.
			// This needs to be done because, if loaded from a save there will exist
			// a kind of mech-voodoo situation.
			let mechData = I_SaveMechData(plr.FindInventory("I_SaveMechData"));
			if(mechData) mechData.RecycleActiveMechs();
			
			RestoreActiveMechs();
		}
		else
		{
			level.NextMap = inv.dropshipNextMap;
		}
	}
	
	override void RenderOverlay(RenderEvent e)
	{	
		if(scrfade > 0.01)
		{
			Screen.Dim(0xFF000000, scrfade, 0,0, Screen.getWidth(), Screen.getHeight());
			
			if(scrfade >= 1.0 && !onFirstMap) 
			{
				double loadAlpha = 0.5 + (cos(level.maptime * 20.0) * 0.5); 
				MWDrawer.DrawString("ConsoleFont", "LOADING", (0,0), MWDrawer.SS_SCREEN_CENTER | MWDrawer.SS_TEXT_CENTER, Font.CR_Purple, alpha:loadAlpha);
			}
		}
	}
		
	override void UITick()
	{	
		if(gamestate != GS_LEVEL) return;
	
		let activeMenu = Menu.getCurrentMenu();
		let mechlabMenu = MechMenu_Mechlab(activeMenu);
		
		// Mark mechlab as closed.
		if( usingMechlab && !mechlabMenu)
			EventHandler.SendNetworkEvent("mw_status_closedmechlab");
		
		// Mark mechlab as opened
		if(!usingMechlab && mechlabMenu)
			EventHandler.SendNetworkEvent("mw_status_openedmechlab");
			
		
		// Draw mechbay info
		Canvas cv = TexMan.GetCanvas("MechbayInfoCanvas");
		cv.ClearScreen();
		
		let plr = players[consoleplayer].mo;
		let mechData = I_SaveMechData(plr.FindInventory("I_SaveMechData"));
		if(!mechData) return;
			
		for(int i = 0; i < 10; i++)
		{
			// Find mech associated with this mechbay.
			let mech = mechData.getMechbayMech(i+1);
			
			double x = 240 * i;
			
			if(!mech)
			{
				cv.DrawText("ConsoleFont", 
					Font.CR_White, 
					x + 30, 80, 
					"Mechbay Empty", 
					DTA_ScaleX, 2.0, DTA_ScaleY, 4.5
				);
				continue;
			}
				
			// Vitals
			double ArmorHD, ArmorCT, ArmorLT, ArmorRT, ArmorLA, ArmorRA, ArmorLL, ArmorRL;
			double InternalHD, InternalCT, InternalLT, InternalRT, InternalLA, InternalRA, InternalLL, InternalRL;
			if(mech.head) 	   [ArmorHD, InternalHD] = mech.head.getVitals();
			if(mech.torso)     [ArmorCT, InternalCT] = mech.torso.getVitals();
			if(mech.leftTorso) [ArmorLT, InternalLT] = mech.leftTorso.getVitals();
			if(mech.rightTorso)[ArmorRT, InternalRT] = mech.rightTorso.getVitals();
			if(mech.leftArm)   [ArmorLA, InternalLA] = mech.leftArm.getVitals();
			if(mech.rightArm)  [ArmorRA, InternalRA] = mech.rightArm.getVitals();
			if(mech.leftLeg)   [ArmorLL, InternalLL] = mech.leftLeg.getVitals();
			if(mech.rightLeg)  [ArmorRL, InternalRL] = mech.rightLeg.getVitals();
			double armorPercent = (ArmorHD + ArmorCT + ArmorLT + ArmorRT + ArmorLA + ArmorRA + ArmorLL + ArmorRL) / 8.0;
			double internalPercent = (InternalHD + InternalCT + InternalLT + InternalRT + InternalLA + InternalRA + InternalLL + InternalRL) / 8.0;
			
			string mechName = String.Format("%s - %s", mech.mechChassisName, mech.mechVariantName); 
			
			
			string armorString = String.Format("\c[Blue]Armor %d%%", armorPercent * 100);
			string healthString = String.Format("\c[Red]Internals %d%%", internalPercent * 100);
			
			cv.DrawText("ConsoleFont", Font.CR_White, x + 20, 20, mechName, DTA_ScaleY, 2.5);
			cv.DrawText("ConsoleFont", Font.CR_White, x + 20, 60, armorString, DTA_ScaleY, 2.5);
			cv.DrawText("ConsoleFont", Font.CR_White, x + 20, 90, healthString, DTA_ScaleY, 2.5);
		}
	}
	
	override void NetworkProcess(ConsoleEvent e)
	{	
		PlayerPawn plr = players[e.Player].mo;
		if(!plr) return;
		
		Array<String> args;
		string cmd = e.Name;
		cmd.Split(args, ":");
	
		// TODO: Make this Multiplayer compatible via inventory item boolean per-player.
		// 2023 and we still using inventory hacks, ZScript FTW.
		
		if(cmd ~== "mw_status_openedmechlab")
			usingMechlab = true;
		
		if(cmd ~== "mw_status_closedmechlab")
			usingMechlab = false;
			
		if(cmd ~== "mw_debugsellmech")
		{
			let mechData = I_SaveMechData(plr.FindInventory("I_SaveMechData"));
			if(!mechData) return;
			mechData.RemoveMech(2);
		}
		
		if(args.Size() > 1)
		{
			if(args[0] ~== "loadmech")
			{
				let mechData = I_SaveMechData(plr.FindInventory("I_SaveMechData"));
				if(!mechData) return;
				
				int mechbayID = args[1].ToInt();
				
				vector3 spawnPos = level.vec3offset(plr.pos, (cos(plr.angle), sin(plr.angle), 0) * ((plr.radius*2) + 64));
				mechData.loadMechData(mechbayID, spawnPos);
			}	
		}
	}
}

// Dropship mechbay callbacks
class MechbayScripts abstract play
{
	static int OpenMechlab(Actor activator, int mechbayID)
	{
		let plr = PlayerPawn(activator);
		let evh = MechInteraction_Handler(EventHandler.Find("MechInteraction_Handler"));
		
		let mechData = I_SaveMechData(plr.FindInventory("I_SaveMechData"));
		DrivableMech mech = mechData.getMechbayMech(mechbayID);
		if(!mech || !plr) return 0;
		
		evh.OpenMechlab(plr.PlayerNumber(), mech);
		return 1;
	}
	
	static void OpenMarketplace(Actor activator)
	{
		let plr = PlayerPawn(activator);
		let evh = MechInteraction_Handler(EventHandler.Find("MechInteraction_Handler"));
		
		evh.OpenShop(plr.PlayerNumber());	
	}
	
	static void SaveMechbayData(Actor activator)
	{
		let it = ThinkerIterator.Create("MechBay");
		MechBay bay;
		while(bay = MechBay(it.Next()))
		{
			if(bay.mechData && bay.mech)
				bay.mechData.SaveMechData(bay.mech);
		}
	}
	
	static bool IsMechlabActive(Actor activator)
	{
		let evh = MechDropship_Handler(StaticEventHandler.Find("MechDropship_Handler"));
		return !evh ? false : evh.usingMechlab;
	}
	
	static void SetActiveMech(Actor activator, int mechbayID)
	{
		let mechData = I_SaveMechData(activator.FindInventory("I_SaveMechData"));
		if(!mechData) return;
		console.printf("Setting active mech to #%d", mechbayID);
		mechData.activeMechID = mechbayID;
	}
}

// Mechbay actors
class MechBay : EffectsActor
{
	int mechbayID;
	I_SaveMechData mechData;
	DrivableMech mech;

	override void Activate(Actor activator)
	{
		bInvisible = true;
		
		mechbayID = args[0];
		mechData = I_SaveMechData(activator.FindInventory("I_SaveMechData"));
		if(!mechData) return;
		
		// Load mech
		mech = mechData.LoadMechData(mechbayID, pos, angle);
	}

	States
	{
		Spawn:
			PLSS A -1;
		stop;
	}
}