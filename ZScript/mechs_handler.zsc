class MechInteraction_Handler : EventHandler
{
	string dropshipMap;
	DrivableMech mechlab_curmech;
	Actor mechlab_invsource;

	// Setup MechItems for players
	override void WorldTick()
	{
		for(int i = 0; i < MAXPLAYERS; i++)
		{
			PlayerInfo plrInfo = players[i];
			if(!plrInfo || !playerInGame[i]) continue;
			let plr = players[i].mo;
			I_MechItemStorage.Get(plr);
		}	
	}
	
	play void OpenMechlab(DrivableMech mech, PlayerPawn invSource)
	{
		mechlab_invsource = invSource;
		mechlab_curmech = mech;
		Menu.SetMenu("MechMenu_Mechlab");
		SendInterfaceEvent(invSource.PlayerNumber(), "mechlab.update");
	}
	
	override void WorldUnloaded(WorldEvent e)
	{
		// Call vehicle travelling virtuals
		let it = ThinkerIterator.Create("DrivableObject");
		DrivableObject vehicle;
		while(vehicle = DrivableObject(it.Next())) 
			vehicle.PreTravelled();
	}
	
	override void InterfaceProcess(ConsoleEvent e)
	{	
		string cmd = e.Name;
	
		let mechlab = MechMenu_Mechlab(Menu.getCurrentMenu());
		if(mechlab) 
		{
			mechlab.processPlayEvent(e.Player, e.Name);
			
			if(cmd == 'mechlab.update')
			{
				mechlab.invSource = mechlab_invsource;
				mechlab.mech = mechlab_curmech;
				mechlab.InitElements();
			}
		}
	}
		
	// Moar keys
	override void NetworkProcess(ConsoleEvent e)
	{
		PlayerInfo plr = players[e.Player];
		if(!plr || !plr.mo) return;
		
		let drvObj = I_DrivingObject.Get(plr.mo);
		string cmd = e.Name;
		
		if(drvObj && drvObj.source) 
		{
			bool btnState = cmd.IndexOf("+") != -1 ? true : false; 
			cmd.Remove(0,1);
			cmd = cmd.MakeLower();
			
			if(cmd ~== "mw_groupprev" ) drvObj.source.setExtBtn(DrivableMech.BT_GROUPPREV, btnState);
			if(cmd ~== "mw_groupnext" ) drvObj.source.setExtBtn(DrivableMech.BT_GROUPNEXT, btnState);
			if(cmd ~== "mw_weaponprev") drvObj.source.setExtBtn(DrivableMech.BT_WEAPPREV,  btnState);
			if(cmd ~== "mw_weaponnext") drvObj.source.setExtBtn(DrivableMech.BT_WEAPNEXT,  btnState);
			
			if(cmd ~== "mw_grouptoggle"   ) drvObj.source.setExtBtn(DrivableMech.BT_GROUPSET,  btnState);
			if(cmd ~== "mw_groupchainfire") drvObj.source.setExtBtn(DrivableMech.BT_GROUPCHF,  btnState);
			
			if(cmd ~== "mw_armlock"   ) drvObj.source.setExtBtn(DrivableMech.BT_ARMLOCK,    btnState);
			if(cmd ~== "mw_torsolock" ) drvObj.source.setExtBtn(DrivableMech.BT_TORSOLOCK,  btnState);
			if(cmd ~== "mw_alignlegs" ) drvObj.source.setExtBtn(DrivableMech.BT_ALIGNLEGS,  btnState);
			if(cmd ~== "mw_aligntorso") drvObj.source.setExtBtn(DrivableMech.BT_ALIGNTORSO, btnState);
			if(cmd ~== "mw_stopmech"  ) drvObj.source.setExtBtn(DrivableMech.BT_STOPMECH,   btnState);
			if(cmd ~== "mw_peekinventory") drvObj.source.setExtBtn(DrivableMech.BT_PEEKINV, btnState);
			
			if(cmd ~== "mw_gettarget")  drvObj.source.setExtBtn(DrivableMech.BT_GETTARGET, btnState);
			if(cmd ~== "mw_powercycle") drvObj.source.setExtBtn(DrivableMech.BT_POWERCYCLE, btnState);
			if(cmd ~== "mw_overrideshutdown") drvObj.source.setExtBtn(DrivableMech.BT_OVRSHUTDWN, btnState);
			
			if(cmd ~== "mw_openmechlab" && e.Player == consoleplayer)
			{
				OpenMechlab(DrivableMech(drvObj.source), plr.mo);
			}
		}
				
		if(mechlab_curmech && mechlab_invsource)
			MechMenu_Mechlab.processMechlabEvents(e, PlayerPawn(mechlab_invsource), mechlab_curmech);
		
		// Cheats and Debug
		Array<string> args;
		cmd.Split(args, ".");
		cmd = args[0];
		
		if(cmd ~== "inv")
		{
			let inv = I_MechItemStorage.Get(plr.mo, give:false);
			let item = (class<MechItem>)(args[2]);
			if(!inv || !item) return;
			
			if(args[1] == "give")
			{
				inv.AddItem(MechItem.Init(item));
				console.printf("Received: %s.", item.getClassName());
			}
		}
	}
}